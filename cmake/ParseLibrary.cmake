## Parse old_library/dependency_libs from *.la 
function(ParseLaDependencies out_lib out_deps fname rpaths)
    set(la_path "${fname}")
    if (NOT EXISTS "${fname}")
        unset(la_path)
        find_file(la_path NAMES ${fname} PATHS ${rpaths} NO_CACHE)
    endif()
    file(READ "${la_path}" la_contents)

    set(${out_lib} "")
    if (la_contents MATCHES "old_library *= *'([^']*)'")
        string(STRIP "${CMAKE_MATCH_1}" old_library)
        find_library(target_lib NAMES ${old_library} PATHS ${rpaths} NO_CACHE)
        set(${out_lib} "${target_lib}" PARENT_SCOPE)
    endif()

    set(${out_deps} "")
    if (la_contents MATCHES "dependency_libs *= *'([^']*)'")
        string(STRIP "${CMAKE_MATCH_1}" dependency_libs)
        string(REGEX REPLACE " +" ";" target_deps "${dependency_libs}")
        set(${out_deps} "${target_deps}" PARENT_SCOPE)
    endif()
endfunction()


## Parse static link libraries
function(ParseStaticLibrary out_result fname rpaths)
    if (fname MATCHES ".*\.la$")
        list(APPEND la_names "${fname}")
        list(LENGTH la_names la_length)
    elseif (fname MATCHES ".*\.a$")
        find_library(target_lib NAMES ${fname} PATHS ${rpaths} NO_CACHE)
        list(APPEND out_libs "${target_lib}")
    endif()

    while (la_length GREATER 0) 
        unset(tmp_names)
        foreach (tmp_name IN ITEMS ${la_names})
            unset(tmp_path)
            unset(tmp_deps)
            ParseLaDependencies(tmp_path tmp_deps ${tmp_name} "${rpaths}")

            list(APPEND out_libs "${tmp_path}")
            foreach (item IN ITEMS ${tmp_deps})
                if (item MATCHES ".*\.la$")
                    list(APPEND tmp_names "${item}")
                else()
                    list(APPEND out_deps "${item}")
                endif()
            endforeach()
        endforeach()
        set(la_names ${tmp_names})
        list(LENGTH la_names la_length)
    endwhile()

    list(REMOVE_DUPLICATES out_libs)
    list(REVERSE out_deps)
    list(REMOVE_DUPLICATES out_deps)
    list(REVERSE out_deps)
    set(${out_result} ${out_libs} ${out_deps} PARENT_SCOPE)
endfunction()
